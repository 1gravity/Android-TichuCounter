plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion = 30

    defaultConfig {
        applicationId "com.onegravity.tichucount"
        minSdkVersion 18
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        android.defaultConfig.vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "process"

    productFlavors {
        // The actual release version
        production {
            dimension "process"
        }

        // The development version
        development {
            dimension "process"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    buildFeatures {
        viewBinding true
    }

    // let's put the variant name and the version number into the generated apk
    android.applicationVariants.all { variant ->
        project.logger.lifecycle('  processing variant: ' + variant.versionName)

        def appName
        //Check if an applicationName property is supplied; if not use the name of the parent project.
        if (project.hasProperty("applicationName")) {
            appName = applicationName
        } else {
            appName = parent.name
        }

        variant.outputs.all { output ->
            def lintTask = tasks["lint${variant.name.capitalize()}"]
            output.assemble.dependsOn lintTask

            def newApkName

            //If there's no ZipAlign task it means that our artifact will be unaligned and we need to mark it as such.
            newApkName = "${appName}-${variant.versionName}.apk"
            outputFileName = new File(newApkName)
        }
    }
}

dependencies {
    implementation 'com.android.support:multidex:1.0.3'

    // base libraries
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.21"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // Conductor
    implementation 'com.bluelinelabs:conductor:3.0.0'
    implementation 'com.bluelinelabs:conductor-viewpager2:3.0.0'

    // RxJava
    implementation "io.reactivex.rxjava3:rxjava:3.0.7"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation "com.github.akarnokd:rxjava3-extensions:3.0.1"

    // Toothpick
    implementation 'com.github.stephanenicolas.toothpick:ktp:3.1.0'
    kapt 'com.github.stephanenicolas.toothpick:toothpick-compiler:3.1.0'
    testImplementation 'com.github.stephanenicolas.toothpick:toothpick-testing-junit5:3.1.0'

    // Room
    implementation "androidx.room:room-runtime:2.3.0-alpha04"
    implementation "androidx.room:room-ktx:2.3.0-alpha04"
    implementation 'androidx.room:room-rxjava3:2.3.0-alpha04'
    kapt "androidx.room:room-compiler:2.3.0-alpha04"

    // Stetho
    developmentApi "com.facebook.stetho:stetho:1.5.1"
    developmentImplementation("com.facebook.stetho:stetho-js-rhino:1.5.1")

    // Testing
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'org.mockito:mockito-core:2.27.0'
    testImplementation 'androidx.test:core:1.3.0'
}